%prefixes%

	terminal = tok_;
	function = p_;

%persistents%

	lex_state :lex_state;
	act_state :act_state;

%maps%

	string -> string;
	scope  -> scope;
	ast    -> ast;

%header% @{

	#include <assert.h>
	#include <stdio.h>

	#include "lex.h"
	#include "ast.h"
	#include "scope.h"
	#include "parser.h"

	struct act_string {
		const char *s;
		const char *e;
	};

	struct act_state {
		struct lex_tok t;
		enum lex_type type, save;
		int (*dispatch)(struct ast *);
		struct scope *k;
	};

	typedef struct act_string string;

	#define CURRENT_TERMINAL act_state->type
	#define ERROR_TERMINAL   tok_error
	#define ADVANCE_LEXER    lex_next(lex_state, &act_state->t); \
							 act_state->type = act_state->t.type;
	#define SAVE_LEXER(tok)  act_state->save = tok;
	#define RESTORE_LEXER    act_state->type = act_state->save;

	static struct ast *
	binop(enum ast_type type, struct ast *a, struct ast *b)
	{
		if (a == NULL) {
			return b;
		}

		if (b == NULL) {
			return a;
		}

		return ast_new_op(type, a, b);
	}

@}, @{

	#include <stdio.h>

	typedef struct lex_state * lex_state;
	typedef struct act_state * act_state;
	typedef struct ast *       ast;

	int
	parse(struct lex_state *l,
		int (*dispatch)(struct ast *));

@};

%terminals%

	str: () -> (s :string) = @{
		assert(act_state->t.s != NULL);
		assert(act_state->t.e >= act_state->t.s);

		@s.s = act_state->t.s;
		@s.e = act_state->t.e;
	@};

%actions%

	<free>: (q :ast) -> () = @{
		assert(@q != NULL);

		ast_free(@q);
	@};

	<dispatch>: (q :ast) -> () = @{
		if (-1 == act_state->dispatch(@q)) {
			@!;
		}
	@};

	<epsilon>: () -> (q :ast) = @{
		@q = NULL;
	@};

	<scope-push>: () -> () = @{
		if (!scope_push(&act_state->k)) {
			@!;
		}
	@};

	<scope-pop>: () -> () = @{
		(void) scope_pop(&act_state->k);
	@};

	<new-str>: (s :string) -> (q :ast) = @{
		@q = ast_new_leaf(AST_STR, @s.e - @s.s, @s.s);
		if (@q == NULL) {
			@!;
		}
	@};

	<block-scope>: (a :ast) -> (q :ast) = @{
		@q = ast_new_block(AST_BLOCK, act_state->k, @a);
		if (@q == NULL) {
			ast_free(@a);
			@!;
		}
	@};

	<block-runfg>: (a :ast) -> (q :ast) = @{
		@q = ast_new_block(AST_RUNFG, act_state->k, @a);
		if (@q == NULL) {
			ast_free(@a);
			@!;
		}
	@};

	<block-runbg>: (a :ast) -> (q :ast) = @{
		@q = ast_new_block(AST_RUNBG, act_state->k, @a);
		if (@q == NULL) {
			ast_free(@a);
			@!;
		}
	@};

	<binop-and>: (a :ast, b :ast) -> (q :ast) = @{
		@q = binop(AST_AND, @a, @b);
		if (@q == NULL) {
			ast_free(@a);
			ast_free(@b);
			@!;
		}
	@};

	<binop-or>: (a :ast, b :ast) -> (q :ast) = @{
		@q = binop(AST_OR, @a, @b);
		if (@q == NULL) {
			ast_free(@a);
			ast_free(@b);
			@!;
		}
	@};

	<binop-join>: (a :ast, b :ast) -> (q :ast) = @{
		@q = binop(AST_JOIN, @a, @b);
		if (@q == NULL) {
			ast_free(@a);
			ast_free(@b);
			@!;
		}
	@};

	<binop-pipe>: (a :ast, b :ast) -> (q :ast) = @{
		@q = binop(AST_PIPE, @a, @b);
		if (@q == NULL) {
			ast_free(@a);
			ast_free(@b);
			@!;
		}
	@};

	<binop-assign>: (a :ast, b :ast) -> (q :ast) = @{
		@q = binop(AST_ASSIGN, @a, @b);
		if (@q == NULL) {
			ast_free(@a);
			ast_free(@b);
			@!;
		}
	@};

	<binop-sep>: (a :ast, b :ast) -> (q :ast) = @{
		@q = binop(AST_SEP, @a, @b);
		if (@q == NULL) {
			ast_free(@a);
			ast_free(@b);
			@!;
		}
	@};

	<binop-cons>: (a :ast, b :ast) -> (q :ast) = @{
		@q = binop(AST_CONS, @a, @b);
		if (@q == NULL) {
			ast_free(@a);
			ast_free(@b);
			@!;
		}
	@};

%trailer% @{

	int
	parse(struct lex_state *lex_state,
		int (*dispatch)(struct ast *))
	{
		struct act_state a, *act_state = &a;

		assert(lex_state != NULL);
		assert(dispatch != NULL);

		act_state->dispatch = dispatch;
		act_state->k        = NULL;

		lex_state->p = lex_state->buf;

		ADVANCE_LEXER;  /* XXX: what if the first token is unrecognised? */

		/* TODO: handle raising an error or ## */
		p_script(lex_state, act_state);

		return 0;
	}

@}, @{

@};

