%types%

	string;
	!frame;
	type;
	code;
	pipe;
	fd;

%terminals%

	!error;
	!panic;

	eof;
	nl;
	semi;
	bg;
	equ;
	comma;
	!dot;
	tick;
	bar;
	obrace;  cbrace;
	oparen;  cparen;
	osquare; csquare;
	or;
	and;
	!exec;

	str: () -> (:string);
	var: () -> (:string);

%productions%

	<dispatch>: (:code &) -> ();

	<var-top>:  () -> (:string);

	<empty-code>: () -> (:code);
	<empty-pipe>: () -> (:pipe);

	<stdin>:   () -> (:fd);
	!<stdout>: () -> (:fd);
	!<stderr>: () -> (:fd);

	<type-null>: () -> (:type);
	<type-anon>: () -> (:type);
	<type-ret>:  () -> (:type);
	<type-not>:  () -> (:type);
	<type-tick>: () -> (:type);
	<type-call>: () -> (:type);
	<type-exec>: () -> (:type);
	<type-if>:   () -> (:type);
	!<type-join>:() -> (:type);
	<type-pipe>: () -> (:type);
	<type-set>:  () -> (:type);

	<make-fd>:   (:string) -> (:fd);
	<make-pipe>: (:pipe &, :fd, :fd) -> ();

	<prepend-anon>: (:code &, :type, :code) -> ();
	<prepend-data>: (:code &, :string)      -> ();
	<prepend-code>: (:code &, :type)        -> ();

	<append-anon>:  (:code &, :type, :code) -> ();
	<append-data>:  (:code &, :string)      -> ();
	<append-pipe>:  (:code &, :pipe)        -> ();
	<append-code>:  (:code &, :type)        -> ();
	<append-cat>:   (:code &, :code)        -> ();

	<frame-push>;
	<frame-pop>;
	<frame-set>: (:string, :code) -> ();

	<err-expected-sep>;
	<err-syntax>: (:code) -> ();

	tick-value: (:code &) -> ();
	block:      (:code &) -> ();
	exprs:      (:code &) -> ();

	arg-list: (c :code &) -> () [

		::arg: (c :code &) -> () = {
			s = str;
			<prepend-data>(&c, s);
		||
			t = <type-call>;
			<prepend-code>(&c, t);

			s = var;
			<prepend-data>(&c, s);
		};

	] = {
		{
			arg(&c);
		||
			tick-value(&c);
		};

		{
			arg-list(&c);
		||
			$;
		};
	};

	assign-expr: (c :code &) -> () [

		::value: (c :code &) -> () = {
			oparen;
			arg-list(&c);
			cparen;
		||
			block(&c);
		||
			arg(&c);
		};

		::tick-value: (c :code &) -> () = {
			tick;

			t2 = <type-null>;
			<prepend-code>(&c, t2);

			{
				arg(&c);
			||
				ci = <empty-code>;

				block(&ci);

				t0 = <type-ret>;
				<append-code>(&ci, t0);

				/* TODO: get rid of #anon and have #tick take @ci here */
				t3 = <type-anon>;
				<prepend-anon>(&c, t3, ci);

				/* XXX: would need to set up pipe for ` here, first. inner #exec are not dup2'd */
				/* XXX: maybe best to have #exec take a u.pipe argument for dup2 */
			};

			t1 = <type-tick>;
			<prepend-code>(&c, t1);
		};

	] = {
		s = str;
		equ;

		ci = <empty-code>;

		{
			value(&ci);
		||
			tick-value(&ci);
		};

		t0 = <type-ret>;
		<append-code>(&ci, t0);

		<append-data>(&c, s);

		t1 = <type-set>;
		<append-anon>(&c, t1, ci);
	};

	block-expr: (c :code &) -> () [

		::block: (c :code &) -> () = {
			obrace;
			<frame-push>;
			{
				exprs(&c);
			||
				$;
			};
			<frame-pop>;
			cbrace;
		};

	] = {
		ci = <empty-code>;

		/*
		 * SID's parsing has single-token lookahead. This has the effect that
		 * actions must come before disambiguating the next token. Because LL(1)
		 * parsing does not permit leftmost recursion, the <stack-push> actions
		 * must be called after recurring - i.e. in reverse order.
TODO: update comment:
		 * So <prepend-stack> is expected to wind instructions in reverse order,
		 * to counter that &ci is constructed backwards.
		 */

		{
			arg-list(&ci);

			t0 = <type-null>;
			<prepend-code>(&ci, t0);

			t1 = <type-exec>;
			<append-code>(&ci, t1);

			<append-cat>(&c, ci);
		||
			block(&c);
		||
			assign-expr(&c);
		};
	};

	pipe-expr: (c :code &) -> () [

		/* here I would use zones if the lexer supported it, and emit tok_fd */
		redir: () -> (l :fd, r :fd) = {
			n = str;
			equ;
			m = str;

			l = <make-fd>(n);
			r = <make-fd>(m);
		||
			n = str;

			l = <make-fd>(n);
			r = <stdin>;
		};

		redir-list: (p :pipe &) -> () = {
			(l, r) = redir;
			<make-pipe>(&p, l, r);

			{
				comma;
				redir-list(&p);
			||
				$;
			};
		};

	] = {
		block-expr(&c);

		{
			pi = <empty-pipe>;

			bar;
			{
				osquare;
				redir-list(&pi);
				csquare;
			||
				$;
			};

			t = <type-pipe>;
			<append-pipe>(&c, pi);

			ci = <empty-code>;
			pipe-expr(&ci);

			<append-cat>(&c, ci);
		||
			$;
		};
	};

	and-expr: (c :code &) -> () = {
		pipe-expr(&c);

		{
			and;

			ci = <empty-code>;
			and-expr(&ci);

			t0 = <type-ret>;
			<append-code>(&ci, t0);

			t2 = <type-if>;
			<append-anon>(&c, t2, ci);
		||
			$;
		};
	};

	or-expr: (c :code &) -> () = {
		and-expr(&c);

		{
			or;

			ci = <empty-code>;
			or-expr(&ci);

			t0 = <type-ret>;
			<append-code>(&ci, t0);

			t1 = <type-not>;
			<append-code>(&c, t1);

			t2 = <type-if>;
			<append-anon>(&c, t2, ci);
		||
			$;
		};
	};

	exprs: (c :code &) -> () [

		::sep = {
			nl;
		||
			semi;
		||
			bg;
			/* TODO: <code-bg>(c); and set $% as jobid */
		##
			<err-expected-sep>;
		};

		::expr: (c :code &) -> () = {
			or-expr(&c);
		##
			<err-syntax>(c);
		};

	] = {
		expr(&c);

		{
			sep;

			ci = <empty-code>;
			exprs(&ci);

			<append-cat>(&c, ci);
		||
			sep;
		||
			$;
		};
	};

	script [

		global-exprs = {
			c = <empty-code>;

			expr(&c);
			sep;

			<dispatch>(&c);

			s = <var-top>;
			<frame-set>(s, c);

			{
				global-exprs;
			||
				$;
			};
		};

	] = {
		<frame-push>;
		{
			global-exprs;
		||
			$;
		};
		<frame-pop>;

		eof;
	};

%entry%

	script;

