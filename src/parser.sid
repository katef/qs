%types%

	string;
	!scope;
	ast;

%terminals%

	!error;
	!panic;

	eof;
	nl;
	semi;
	bg;
	equ;
	!dot;
	!back;
	!var;
	pipe;
	obrace;
	cbrace;
	oparen;
	cparen;
	or;
	and;
	!exec;

	str: () -> (:string);

%productions%

	<free>:     (:ast) -> ();
	<dispatch>: (:ast) -> ();

	<epsilon>: () -> (:ast);

    <scope-push>;
    <scope-pop>;

	<new-str>: (:string) -> (:ast);

	<binop-and>:    (:ast, :ast) -> (:ast);
	<binop-or>:     (:ast, :ast) -> (:ast);
	!<binop-join>:  (:ast, :ast) -> (:ast);
	<binop-pipe>:   (:ast, :ast) -> (:ast);
	<binop-assign>: (:ast, :ast) -> (:ast);
	<binop-exec>:   (:ast, :ast) -> (:ast);
	<binop-bg>:     (:ast, :ast) -> (:ast);
	<binop-cons>:   (:ast, :ast) -> (:ast);

	/* TODO: <free> */

	exprs: () -> (:ast);

	list: () -> (q :ast) [

		term: () -> (q :ast) = {
			s = str;
			q = <new-str>(s);
		};

	] = {
		a = term;

		{
			b = list;
		||
			b = <epsilon>;
		};

		q = <binop-cons>(a, b);
	};

	cmd: () -> (q :ast) = {
		q = list;
	||
		obrace;
		<scope-push>;
		q = exprs;
		<scope-pop>;
		cbrace;
	||
		oparen;
		q = exprs;
		cparen;
	};

	assign-expr: () -> (q :ast) = {
		a = cmd;

		{
			equ;
			b = assign-expr;
		||
			b = <epsilon>;
		};

		q = <binop-assign>(a, b);
	};

	pipe-expr: () -> (q :ast) = {
		a = assign-expr;

		{
			pipe;
			b = pipe-expr;
		||
			b = <epsilon>;
		};

		q = <binop-pipe>(a, b);
	};

	and-expr: () -> (q :ast) = {
		a = pipe-expr;

		{
			and;
			b = and-expr;
		||
			b = <epsilon>;
		};

		q = <binop-and>(a, b);
	};

	or-expr: () -> (q :ast) = {
		a = and-expr;

		{
			or;
			b = or-expr;
		||
			b = <epsilon>;
		};

		q = <binop-or>(a, b);
	};

	exprs: () -> (q :ast) [

		::expr: () -> (q :ast) = {
			a = or-expr;
			b = <epsilon>;

			{
				nl;
				q = <binop-exec>(a, b);
			||
				q = <binop-exec>(a, b);
				semi;
			||
				q = <binop-bg>(a, b);
				bg;
			};

		||
			q = <epsilon>;
			nl;
		||
			q = <epsilon>;
			semi;
		};

	] = {
		a = expr;

		{
			b = exprs;
		||
			b = <epsilon>;
		};

		q = <binop-exec>(a, b);
	};

	script [

		global-exprs = {
			q = expr;

			<dispatch>(q);
			<free>(q);

			{
				global-exprs;
			||
				$;
			};
		};

	] = {
		<scope-push>;
		global-exprs;
		<scope-pop>;

		eof;
	};

%entry%

	script;

