%types%

	string;
	!frame;
	type;
	code;

%terminals%

	!error;
	!panic;

	eof;
	nl;
	semi;
	bg;
	equ;
	!dot;
	!back;
	pipe;
	obrace;
	cbrace;
	oparen;
	cparen;
	or;
	and;
	!exec;

	str: () -> (:string);
	var: () -> (:string);

%productions%

	<dispatch>: (:code &) -> ();
	<populate>;

	<var-self>: () -> (:string);
	<var-top>:  () -> (:string);

	<empty>: () -> (:code);

	<type-null>: () -> (:type);
	<type-not>:  () -> (:type);
	<type-call>: () -> (:type);
	<type-exec>: () -> (:type);
	<type-if>:   () -> (:type);
	!<type-join>:() -> (:type);
	<type-pipe>: () -> (:type);
	<type-set>:  () -> (:type);

	<push-anon>: (:code &, :code)   -> ();
	<push-data>: (:code &, :string) -> ();
	<push-code>: (:code &, :type)   -> ();
	<push-stack>:(:code &, :code)   -> ();

	<frame-push>;
	<frame-pop>;
	<frame-set>: (:string, :code) -> ();

	exprs:     (:code &) -> ();
	exec-list: (:code &) -> ();

	compound-expr: (c :code &) -> () = {
		oparen;
		exec-list(&c);
		cparen;
	||
		obrace;
		<frame-push>;
		ci = <empty>;
		exprs(&ci);
		s = <var-self>;
		<frame-set>(s, ci);
		<frame-pop>;
		cbrace;
	};

	value: (c :code &) -> () = {
		compound-expr(&c);
	||
		s = str;
		<push-data>(&c, s);
	||
		t = <type-call>;
		<push-code>(&c, t);
		s = var;
		<push-data>(&c, s);
	};

	exec-list: (c :code &) -> () = {
		/* TODO: || backtick */
		{
			s = str;
			<push-data>(&c, s);
		||
			t = <type-call>;
			<push-code>(&c, t);
			s = var;
			<push-data>(&c, s);
		||
			compound-expr(&c);
		};

		{
			exec-list(&c);
		||
			t = <type-null>;
			<push-code>(&c, t);
		};
	};

	arg-list: (c :code &) -> () = {
		/* TODO: || backtick */
		/* TODO: consolidate with exec-list and value */
		{
			s = str;
			<push-data>(&c, s);
		||
			t = <type-call>;
			<push-code>(&c, t);
			s = var;
			<push-data>(&c, s);
		};

		{
			arg-list(&c);
		||
			t = <type-null>;
			<push-code>(&c, t);
		};
	};

	assign-expr: (c :code &) -> () = {
		ci = <empty>;

		/*
		 * SID's parsing has single-token lookahead. This has the effect that
		 * actions must come before disambiguating the next token. Because LL(1)
		 * parsing does not permit leftmost recursion, the <stack-push> actions
		 * must be called after recurring - i.e. in reverse order.
		 * So <push-stack> is expected to wind instructions in reverse order,
		 * to counter that &ci is constructed backwards.
		 */

		{
			arg-list(&ci);
			<push-stack>(&c, ci);
			t = <type-exec>;
			<push-code>(&c, t);
		||
			s = str;
			equ;
			value(&c);
			<push-data>(&c, s);
			t = <type-set>;
			<push-code>(&c, t);
		};
	};

	pipe-expr: (c :code &) -> () = {
		assign-expr(&c);

		{
			pipe;
			pipe-expr(&c);
			t = <type-pipe>;
			<push-code>(&c, t);
		||
			$;
		};
	};

	and-expr: (c :code &) -> () = {
		pipe-expr(&c);

		{
			and;

			t = <type-if>;
			<push-code>(&c, t);

			ci = <empty>;
			and-expr(&ci);
			<push-anon>(&c, ci);
		||
			$;
		};
	};

	or-expr: (c :code &) -> () = {
		and-expr(&c);

		{
			or;

			t1 = <type-not>;
			<push-code>(&c, t1);

			t2 = <type-if>;
			<push-code>(&c, t2);

			ci = <empty>;
			or-expr(&ci);
			<push-anon>(&c, ci);
		||
			$;
		};
	};

	exprs: (c :code &) -> () [

		::expr: (c :code &) -> () = {
			or-expr(&c);

			{
				nl;
			||
				semi;
			||
				bg;
				/* TODO: <code-bg>(c); and set $% as jobid */
			};
		||
			nl;
		||
			semi;
		};

	] = {
		expr(&c);

		{
			exprs(&c);
		||
			$;
		};
	};

	script [

		global-exprs = {
			c = <empty>;

			expr(&c);

			<dispatch>(&c);

			s = <var-top>;
			<frame-set>(s, c);

			{
				global-exprs;
			||
				$;
			};
		};

	] = {
		<frame-push>;
		<populate>;
		global-exprs;
		<frame-pop>;

		eof;
	};

%entry%

	script;

